prog -> classDeclList progBody 
classDeclList -> classDecl classDeclList | EPSILON

classDecl -> class id { varFuncDeclList } ; 
varFuncDeclList -> EPSILON | type id varFuncDeclListP 
varFuncDeclListP -> arraySizeList ;  varFuncDeclList | ( fParams )  funcBody ;  funcDefList

funcDefList -> funcDef funcDefList  | EPSILON
funcDef -> funcHead funcBody ; 
funcHead -> type id ( fParams ) 

funcBody -> { varStateList } 
varStateList -> EPSILON | id varStateListP 
 | integer id arraySizeList ; varStateList
 | real id arraySizeList ; varStateList
  |  if ( expr ) then statBlock else statBlock ; statementList
  |  while ( expr ) do statBlock ; statementList
  |  read ( variable ) ; statementList
  |  write ( expr ) ; statementList
  |  return ( expr ) ;  statementList
varStateListP -> id arraySizeList ; varStateList | indiceList variableP  assignOp expr ;  statementList

progBody -> program funcBody ; funcDefList
statementList -> statement statementList  | EPSILON
arraySizeList -> arraySize arraySizeList  | EPSILON
statement -> variable assignOp expr ; 
  |  if ( expr ) then statBlock else statBlock ; 
  |  while ( expr ) do statBlock ; 
  |  read ( variable ) ; 
  |  write ( expr ) ; 
  |  return ( expr ) ;  
statBlock -> { statementList } | statement | EPSILON

expr  -> arithExpr exprP
exprP  -> EPSILON | relOp arithExpr

arithExpr -> term arithExprP
arithExprP -> EPSILON | addOp term arithExprP

sign  ->  + | - 

term -> factor termP
termP -> EPSILON | multOp factor termP

factor  -> id factorPP
  |  num 
  |  ( expr ) 
  |  not factor 
  | sign factor 
factorPP -> indiceList factorP | ( aParams )  
factorP -> EPSILON | . id factorPP

variable  ->  id indiceList variableP 
variableP ->  EPSILON | . id indiceList variableP

indiceList -> indice indiceList  | EPSILON
indice  ->  [ arithExpr ] 
arraySize  ->  [ int ] 
type  ->  integer | real | id 
fParams  ->  type id arraySizeList fParamsTailList | EPSILON 
fParamsTailList -> fParamsTail fParamsTailList  | EPSILON
aParams  ->  expr aParamsTailList | EPSILON
aParamsTailList -> aParamsTail aParamsTailList  | EPSILON
fParamsTail  ->  , type id arraySizeList
aParamsTail  -> , expr
